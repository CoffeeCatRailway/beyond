import groovy.json.JsonOutput
import groovy.json.JsonSlurper

buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net' }
        maven { url = 'https://repo.spongepowered.org/maven' }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '4.1.+', changing: true
        classpath 'org.spongepowered:mixingradle:0.7-SNAPSHOT'
    }
}

plugins {
    id 'com.github.johnrengelman.shadow' version '4.0.4'
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.spongepowered.mixin'
apply plugin: 'eclipse'

version = project.modVersion
group = 'io.github.ocelot.' + project.modId
archivesBaseName = project.modId

java.toolchain.languageVersion = JavaLanguageVersion.of(8)

println('Java: ' + System.getProperty('java.version') + ' JVM: ' + System.getProperty('java.vm.version') + '(' + System.getProperty('java.vendor') + ') Arch: ' + System.getProperty('os.arch'))
minecraft {
    mappings channel: project.mappingsChannel, version: project.mappings

    runs {
        client {
            workingDirectory project.file('run/client')
            property 'forge.logging.console.level', 'debug'
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"
            arg "-mixin.config=space.mixins.json"

            mods {
                space {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run/server')
            property 'forge.logging.console.level', 'debug'
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"
            arg "-mixin.config=space.mixins.json"

            mods {
                space {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')
            property 'forge.logging.console.level', 'debug'
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"
            args '--mod', project.modId, '--all', '--output', file('src/generated/resources/')

            mods {
                space {
                    source sourceSets.main
                }
            }
        }
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

mixin {
    add sourceSets.main, "${project.modId}.refmap.json"
}

configurations {
    shade
}

repositories {
//    flatDir {
//        dirs "libs"
//    }
//    mavenCentral()
//    maven {
//        name = "Curse Maven"
//        url = "https://www.cursemaven.com"
//    }
    maven {
        name = "JitPack"
        url = "https://jitpack.io"
    }
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.16.5-36.1.16'

    compileOnly fg.deobf("com.github.Ocelot5836:Sonar:${project.sonar}:api")
    runtimeOnly fg.deobf("com.github.Ocelot5836:Sonar:${project.sonar}")
    shade "com.github.Ocelot5836:Sonar:${project.sonar}"
}

processResources {
    inputs.property("modName", project.modName)
    inputs.property("modVersion", project.modVersion)
    inputs.property("modId", project.modId)
    filesMatching(["META-INF/mods.toml", "pack.mcmeta", (String) "${project.modId}.mixins.json"]) {
        expand 'modVersion': project.modVersion, 'modId': project.modId, 'modName': project.modName
    }

    // Minify json resources
    doLast {
        fileTree(dir: outputs.files.asPath, include: "**/*.json").each {
            File file -> file.text = JsonOutput.toJson(new JsonSlurper().parse(file))
        }
    }
}

jar {
    manifest {
        attributes([
                "Specification-Title"     : "space",
                "Specification-Vendor"    : "Ocelot",
                "Specification-Version"   : "1",
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : archiveVersion,
                "Implementation-Vendor"   : "Ocelot",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                "MixinConfigs"            : "${project.modId}.mixins.json"
        ])
    }
}

shadowJar {
    configurations = [project.configurations.shade]
    relocate 'io.github.ocelot', 'io.github.ocelot.space.lib'
}

reobf {
    shadowJar {}
}

artifacts {
    archives jar
    archives shadowJar
}

jar.finalizedBy('reobfJar')
